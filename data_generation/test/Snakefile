
myconfig = "config.yml"
configfile: myconfig

singularity: "ldcpipe.sif" 


subworkflow dgbworkflow:
    workdir:
        "./dgb-pipeline"
    snakefile:
        "./dgb-pipeline/Snakefile"
    configfile:
        "./dgb-pipeline/config.yml"

subworkflow igbworkflow:
    workdir:
        "./igb-pipeline"
    snakefile:
        "./igb-pipeline/Snakefile"
    configfile:
        "./igb-pipeline/config.yml"


rundir = config["dirname"]

rule all:
    input:
        rundir+"/out_blind.h5",  rundir+"/out_nonblind.h5", 


rule full:
    input:
        expand(rundir+"/{source}-lisanode/{source}-tdi.h5",
               source=list(config["sources"].keys())+["sum", "igb", "dgb"]),
        

rule source_selection:
    input:
        cfg = "param_{source}.yml",
    output:
        rundir+"/{source}.npy"
    shell:
        """
        source_selection -c {input.cfg} -o {output}
        """

rule arm_projection:
    input:
        cfg = "config.yml", 
        src = rundir+"/{source}.npy"
    output:
        temp(rundir+"/{source}--{batch}-y.h5")
    log:
        "log/{source}--{batch}-y.log"
    params:
        cfg = "param_{source}.yml"
    shell:
        """
        arm_projection -i {input.src} -c {input.cfg} -o {output} --log {log} --select {wildcards.batch} --source-config {params.cfg}
        """

rule batch_merge:
    input:
        src = lambda wildcards: [rundir+"/{source}--%d:%d-y.h5"%(i,config["nbatch"]) for i in range(config["nbatch"])],
        cfg = "config.yml"
    output: rundir+"/{source}-y.h5"
    log: "log/{source}-y.log"
    shell:
        """
        strain_combination -i {input.src} -c {input.cfg} -o {output} --log {log}
        """

rule interp_gb: # used for tdi / source type
    input:
        src1 = dgbworkflow(rundir+"/dgb-y.h5"),
        src2 = igbworkflow(rundir+"/igb-y.h5"),
        cfg = "config.yml"
    output:
        out1 = rundir+"/dgb-y.h5",
        out2 = rundir+"/igb-y.h5"
    log:
        "log/interp_gb.log"
    shell:
        """
        strain_combination -i {input.src1} -c {input.cfg} -o {output.out1} --log {log}
        strain_combination -i {input.src2} -c {input.cfg} -o {output.out2} --log {log}
        """
        
rule source_merge:
    input:
        cfg = "config.yml", 
        src = expand(rundir+"/{source}-y.h5",
                     source=list(config["sources"].keys())+["dgb", "igb"])
    output:
        out = rundir+"/sum-y.h5", 
    log:
        "log/source_merged.log"
    shell:
        """
        strain_combination -i {input.src} -c {input.cfg} -o {output.out} --log {log}
        """

rule L0L1:
    input:
        data = rundir+"/{source}-y.h5",
        graph = "LDC_graph.py",
        cfg = myconfig,
        cfgln = "lisanode_config.py",
    output:
        graph = rundir+"/{source}-lisanode/mygraph.py",
        s = rundir+"/{source}-lisanode/{source}-tdi.h5"
    log:
        "log/{source}-tdi.log"
    params:
        prefix = config["ldc_liborbits"],
        duration = config["t_max"]+15,
        optim = 1
    resources:
        mem_mb=30000
    shell:
        """
        prep_lisanode -l {log} -o {output.graph} -c {input.cfgln} -g {input.graph} --pipe-config {input.cfg}
        run_lisanode -O {params.optim} --h5 -l {log} -i {input.data} -o {output.s} -g {output.graph} -c {input.cfg} -n LISAWithGWAndTDI -f '-I{params.prefix}/nodes -L{params.prefix}/lib -I{params.prefix}/lib -I{params.prefix}/../../common/constants -lorbits' --duration {params.duration}
        """

rule blind_release:
    input:
        tdi = rundir+"/sum-lisanode/sum-tdi.h5"
    output: rundir+"/out_blind.h5"
    log:
        "log/blind_release.log"
    shell:
        """
        data_release --data-files {input.tdi} --dset-names tdi-sum -o {output} -l {log}
        """

rule release:
    input:
        tdi = expand(rundir+"/{source}-lisanode/{source}-tdi.h5",
                     source=["sum"]),
        cfg = [myconfig, "lisanode_config.py"] +\
              expand("param_{source}.yml", source=["vgb", "mbhb"]) +\
              [dgbworkflow("param_dgb.yml"), igbworkflow("param_igb.yml")],
        cat = expand(rundir+"/{source}.npy", source=["vgb", "mbhb"]) +\
              [dgbworkflow(rundir+"/dgb.npy"), igbworkflow(rundir+"/igb.npy")]
    output: rundir+"/out_nonblind.h5"
    log:
        "log/release.log"
    shell:
        """
        data_release --data-files {input.tdi} {input.cat} --dset-names obs/tdi sky/vgb/cat sky/mbhb/cat sky/dgb/cat sky/igb/cat --config-files {input.cfg} --config-names obs/config instru/config sky/vgb/config sky/mbhb/config sky/dgb/config sky/igb/config -o {output} -l {log}
        """
    
